require.config do
    baseUrl: '/script'

(window.global)<- require [\global]
`with(global) {`
window.configStorage = Storage.get 'config'

$ window .on \mousedown \.jDragHandler !->
    nativeWindow.startMove!

with globalMenu = new air.NativeMenu
    Item = air.NativeMenuItem
    onsel = air.Event.SELECT

    calcLabel = -> if nativeWindow.alwaysInFront then '[√]总在最上(T)' else '[x]总在最上(T)'
    @addItem new Item calcLabel!
        &addEventListener onsel, (e)->
            nativeWindow.alwaysInFront = !nativeWindow.alwaysInFront 
            e.currentTarget.label = calcLabel!
        &mnemonicIndex = &label.indexOf 'T'

    @addItem new Item '退出(X)'
        &addEventListener onsel, (e)->
            air.NativeApplication.nativeApplication.exit!
        &mnemonicIndex = &label.indexOf 'X'

    $ window .on \contextmenu (e)~>
        @display nativeWindow.stage, e.clientX, e.clientY
        false

new air.Loader
    &contentLoaderInfo.addEventListener air.Event.COMPLETE, (event)->
        air.NativeApplication.nativeApplication.icon.bitmaps = [event.target.content.bitmapData];
    &load new air.URLRequest 'res/icon-rev48.png'
air.NativeApplication.nativeApplication.icon
    &tooltip = "Bangumi Sora"
    &menu = globalMenu
    &addEventListener window.runtime.flash.events.ScreenMouseEvent.MOUSE_DOWN, !(e)->
        nativeWindow.restore!
        nativeWindow.alwaysInFront = !nativeWindow.alwaysInFront 
        nativeWindow.alwaysInFront = !nativeWindow.alwaysInFront 

with shellConf = (configStorage.get \ukagaka_shell) || {
    backgroundImage: 'url(app:/res/ukagaka_default.png)'
    width: 170
    height: 370
    frames: 23
    interval: 100
}
    $uka = $ \.jUkagaka
    $uka.css @
    cur = 0
    setInterval ~>
        cur++
        cur := 0 if(cur >= @frames)
        $uka.css \background-position-x, "-#{cur*@width}px"
    , @interval


`}`